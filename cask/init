#!/usr/bin/env zsh

here=${0:A:h}
source $here/../provision.d/messaging_helpers

errored=false

# abstract away paths
brew=/usr/local/bin/brew
cask_list=$here/casks
postflight=$here/postflight
notes=$here/notes
installed=$here/installed
formula_installed=$here/../brew/installed

# install base system, if not already there
[[ -f $brew ]] || { print_error "/usr/local/bin/brew not found; please install Homebrew first"; exit 1; }
print_milestone "Installing Homebrew Cask base system..."
if brew tap 2>/dev/null | grep -q caskroom/cask; then
    print_progress "Tap 'caskroom/cask' already tapped"
else
    print_command "brew tap caskroom/cask"
    $brew tap caskroom/cask || { print_error "Failed to tap caskroom/cask"; exit 1; }
fi

export HOMEBREW_CASK_OPTS='--appdir=/Applications --verbose'

# casks
print_milestone "Installing casks..."
newly_installed=()
<$cask_list while read -r cask; do
    [[ $cask == \#* ]] && continue # skip comments

    if $installed $cask; then
        print_progress "Cask '$cask' already installed"
    else
        print_command "brew cask install $cask"
        $brew cask install $cask || {
            errored=true
            print_error "Failed to install cask '$cask'"
            continue
        }
        newly_installed+=$cask
        print_progress "Running postflight for cask '$cask'..."
        $postflight $cask || errored=true
    fi
done

if [[ -n $newly_installed ]]; then
    print_milestone "Newly installed casks:"
    print -c $newly_installed
fi

# notes
print_note "$(< $notes)"
print_note "Please run 'postflight --all' if the init script was previously interrupted."

[[ $errored == false ]]

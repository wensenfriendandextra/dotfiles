# -*- mode: sh; -*-

# fetch remote updates to .config in a disowned process so that we can update
# .config as quickly as possible when we want to
git -C ${XDG_CONFIG_HOME:-$HOME/.config} fetch --all &>/dev/null &!

# if local override exists, source the override instead
[[ -e ${XDG_CONFIG_HOME:-$HOME/.config}/zsh/zshrc_override ]] && { source ${XDG_CONFIG_HOME:-$HOME/.config}/zsh/zshrc_override && return; }

# augment fpath
fpath=(${XDG_DATA_HOME:-$HOME/.local/share}/zsh/site-functions $fpath)

# path for zcompdump
ZCOMPDUMP=${XDG_DATA_HOME:-$HOME/.local/share}/zsh/compdump

# prezto
zstyle ':prezto:*:*' color 'yes'
zstyle ':prezto:environment:termcap' color 'no' # disable coloring of less, which looks horrible
zstyle ':prezto:module:editor' key-bindings 'emacs'
(( $+SCRIPT)) || zstyle ':prezto:module:prompt' theme 'zmwangx'
zstyle ':prezto:load' pmodule \
       environment editor history directory utility colors git completion prompt \
       autoenv z homebrew osx diskutil acdcli rm_guard gpg2 \
       haskell \
       local
source ${ZDOTDIR:-$HOME}/.zprezto/init.zsh

function {
    local script private_path

    # source extras
    for script in ${XDG_CONFIG_HOME:-$HOME/.config}/zsh/extras/*.zsh(N); do source $script; done

    # again, $HOME/bin and $HOME/scripts trumps everything else, be it pyenv or rvm
    for private_path in $HOME/bin $HOME/scripts; do
        [[ -d $private_path ]] && PATH=$private_path:$PATH
    done
}

# use simplistic prompt when recording with script(1)
(( $+SCRIPT )) && PROMPT='$ '

# source per-host additions
[[ -e ${XDG_CONFIG_HOME:-$HOME/.config}/zsh/perhostrc ]] && source ${XDG_CONFIG_HOME:-$HOME/.config}/zsh/perhostrc

# remove dupes in paths
typeset -gU cdpath fpath mailpath path

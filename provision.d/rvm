#!/usr/bin/env zsh

here=${0:A:h}
source $here/messaging_helpers

if [[ -e $HOME/.rvm/scripts/rvm ]]; then
    print_progress "RVM already installed in $HOME/.rvm"
else
    print_progress "Retrieving RVM signing key..."
    gpg2 --keyserver hkp://keys.gnupg.net --recv-keys 409B6B1796C275462A1703113804BB82D39DC0E3 || {
        print_error "Failed to retrieve GPG key"
        exit 1
    }
    install_script_url='https://raw.githubusercontent.com/rvm/rvm/master/binscripts/rvm-installer'
    install_script_sig_url='https://raw.githubusercontent.com/rvm/rvm/master/binscripts/rvm-installer.asc'
    install_script_tmp_path=$(mktemp) || {
        print_error "Failed to generate temporary path for RVM install script with mktemp"
        exit 1
    }
    install_script_sig_tmp_path=${install_script_tmp_path}.asc
    trap "rm -f $install_script_tmp_path $install_script_sig_tmp_path" EXIT
    # download install script
    print_progress "Downloading install script and signature..."
    curl -fsSL $install_script_url >$install_script_tmp_path || {
        print_error "Failed to curl RVM install script from $install_script_url"
        exit 1
    }
    # download install script signature
    curl -fsSL $install_script_sig_url >$install_script_sig_tmp_path || {
        print_error "Failed to curl RVM install script signature from $install_script_sig_url"
        exit 1
    }
    # verify signature
    print_progress "Verifying install script signature..."
    gpg2 --verify $install_script_sig_tmp_path || {
        print_error "!!! RVM install script signature verification failed"
        exit 1
    }
    # install
    print_progress "Running install script..."
    bash -- $install_script_tmp_path --user-install --ignore-dotfiles --ruby stable </dev/null || {
        print_error "Failed to install RVM"
        exit 1
    }
fi

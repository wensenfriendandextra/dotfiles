#!/usr/bin/env zsh

# This provisioner makes the necessary adjustments to the stock Zsh global init
# files shipped with OS X.

here=$(perl -e 'use File::Basename; use Cwd "abs_path"; print dirname(abs_path(@ARGV[0]));' -- "$0")
source $here/messaging_helpers

# detect OS X version
osx_version=$(sw_vers -productVersion)
print_progress "OS X $osx_version detected"
osx_major_version=$osx_version[(ws:.:)1]
osx_minor_version=$osx_version[(ws:.:)2]
osx_patch_version=$osx_version[(ws:.:)3]

# If OS X 10.11+, move default /etc/zprofile to /etc/zshenv, which contains
# path initialization with path_helper.
if (( osx_major_version > 10 || ( osx_major_version == 10 && osx_minor_version >= 11 ) )); then
    if [[ -e /etc/zprofile && ! -e /etc/zshenv ]]; then
        print_progress "Moving /etc/zprofile to /etc/zshenv..."
        print_command "sudo mv /etc/zprofile /etc/zshenv"
        sudo mv /etc/zprofile /etc/zshenv || { print_error "Failed to move /etc/zprofile to /etc/zshenv"; exit 1; }
    fi
else
    print_progress "No change needed"
fi

#!/usr/bin/env zsh

here=$(perl -e 'use File::Basename; use Cwd "abs_path"; print dirname(abs_path(@ARGV[0]));' -- "$0")

errored=false

if (( $+commands[brew] )); then
    tapped_taps=( $(brew tap) )
fi

# install Homebrew base system
# redirect stdin to /dev/null to perform a noninteractive install
ruby -e "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)" </dev/null || errored=true

# regenerate paths
source ~/.config/env

# taps
while read -r tap; do
    [[ $tap == \#* ]] && continue # skip comments
    (( $tapped_taps[(I)$tap] )) && continue
    print -P "%B%F{blue}brew tap $tap%f%b" >&2
    brew tap $tap || errored=true
    { echo; echo; echo; } >&2
done < "$here/taps"

# some formulae depend on X11 or JRE, so install brew-cask, xquartz, and java first
$here/installed.py "brew-cask" || {
    print -P "%B%F{blue}brew install brew-cask%f%b" >&2
    brew install brew-cask
}
{ echo; echo; echo; } >&2
print -P "%B%F{blue}brew cask install xquartz java%f%b" >&2
brew cask install xquartz java
{ echo; echo; echo; } >&2

# formulae
while read -r optstring; do
    [[ $optstring == \#* ]] && continue # skip comments
    $here/installed.py $optstring && continue
    print -P "%B%F{blue}brew install $optstring%f%b" >&2
    brew reinstall ${=optstring} || errored=true
    { echo; echo; echo; } >&2
done < "$here/formulae"

# specials
"$here/special" || errored=true

[[ $errored == false ]]

#!/usr/bin/env zsh

here=$(perl -e 'use File::Basename; use Cwd "abs_path"; print dirname(abs_path(@ARGV[0]));' -- "$0")

errored=false

if (( $+commands[brew] )); then
    tapped_taps=( $(brew tap) )
    installed_formulae=( $(brew list -1) )
fi

# install Homebrew base system
# redirect stdin to /dev/null to perform a noninteractive install
ruby -e "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)" </dev/null || errored=true

# regenerate paths
source ~/.config/env

# taps
while read -r tap; do
    [[ $tap == \#* ]] && continue # skip comments
    (( $tapped_taps[(I)$tap] )) && continue
    echo "${BOLD}${BLUE}brew tap $tap${RESET}" >&2
    brew tap $tap || errored=true
    { echo; echo; echo; } >&2
done < "$here/taps"

# some formulae depend on X11 or JRE, so install brew-cask, xquartz, and java first
(( $installed_formulae[(I)brew-cask] )) || {
    echo "${BOLD}${BLUE}brew install brew-cask${RESET}" >&2
    brew install brew-cask
}
{ echo; echo; echo; } >&2
echo "${BOLD}${BLUE}brew cask install xquartz java${RESET}" >&2
brew cask install xquartz java
{ echo; echo; echo; } >&2

# formulae
while read -r optstring; do
    [[ $optstring == \#* ]] && continue # skip comments
    formula_name=$optstring[(w)1] # strip options
    formula_name=$formula_name[(ws:/:)-1] # strip tap prefix
    (( $installed_formulae[(I)$formula_name] )) && continue
    echo "${BOLD}${BLUE}brew install $optstring${RESET}" >&2
    brew install ${=optstring} || errored=true
    { echo; echo; echo; } >&2
done < "$here/formulae"

# specials
"$here/special" || errored=true

[[ $errored == false ]]
